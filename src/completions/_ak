#compdef ak

_ak() {
  local -a commands
  commands=(
    'activities:Show activity stack'
    'clear:Clear app data and cache'
    'devices:Show connected devices'
    'info:Show package information'
    'install:Install APK files'
    'kill:Force-stop package'
    'launch:Launch package'
    'permissions:List package permissions'
    'pull:Pull APK file from device'
    'signature:Extract signature hash'
    'uninstall:Uninstall package'
  )
  
  _arguments -C \
    '(- *)'{-h,--help}'[Show help message]' \
    '(- *)'{-v,--version}'[Show version]' \
    '1:command:->command' \
    '*::arg:->args' && return 0
  
  case $state in
    command)    
      _describe 'ak commands' commands
      ;;
    args)
      case $words[1] in
        activities)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '(-a --all)'{-a,--all}'[Show all tasks]'
          ;;
        clear)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '*:package name:_message "package name"'
          ;;
        devices)
          _arguments '(- *)'{-h,--help}'[Show help for this command]'
          ;;
        info)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name:_message "package name"'
          ;;
        install)
          local -a apk_files
          apk_files=(*.apk(N-.))
          _arguments -C \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '(-a -p)-l[Install latest APK]' \
            '(-l -p)-a[Install all APKs]' \
            '(-l -a)-p[Filter APKs by pattern]:pattern' \
            '-m[Install on all devices]' \
            '-r[Replace existing app]' \
            '-t[Allow test APKs]' \
            '-d[Allow version downgrade]' \
            '*:APK files or directories:_files -g "*.apk(-.)" -/'
          ;;
        kill)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '*:package name:_message "package name"'
          ;;
        launch)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name:_message "package name"'
          ;;
        permissions)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name:_message "package name"'
          ;;
        pull)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name or file name:_message "package name or save file name"' \
            '2:save file name or package name:_files'
          ;;
        signature)
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name or APK file:_files -g "*.apk(-.)"'
          ;;
        uninstall)
          local -a apk_files
          apk_files=(*.apk(N-.))
          _arguments \
            '(- *)'{-h,--help}'[Show help for this command]' \
            '1:package name or APK file:compadd -a apk_files'
          ;;
        *)
          _arguments '(- *)'{-h,--help}'[Show help for this command]'
          ;;
      esac
      ;;
  esac
}

_ak "$@"
